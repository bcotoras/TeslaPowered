plugins {
    id 'org.spongepowered.plugin' version '0.8.1'
    id 'com.github.johnrengelman.shadow' version '1.2.3'
}

group 'com.mcsimonflash.sponge.teslapowered'
version '1.2.2'
ext.spongeversion = '7.1.0-SNAPSHOT'

subprojects {
    apply plugin: 'org.spongepowered.plugin'
    version = "s${spongeversion[0..2]}-v${this.version}"
    dependencies {
        compile "org.spongepowered:spongeapi:${spongeversion}"
    }
}

project(':TeslaLibs') {
    task sourcesJar(type: Jar, dependsOn: classes) {
        from sourceSets.main.allSource
        classifier = 'sources'
    }
    artifacts.archives sourcesJar
}

dependencies {
    compile project(':TeslaCore')
    compile project(':TeslaLibs')
}

shadowJar {
    dependencies {
        include project(':TeslaCore')
        include project(':TeslaLibs')
    }
    archiveName "${project.name}-s${spongeversion[0..2]}-v${version}.jar"
}

task copyJars(type: Copy) {
    from([subprojects.jar, project(':TeslaLibs').sourcesJar, shadowJar])
    into project.file("#releases/${version}")
}

build.dependsOn(copyJars)